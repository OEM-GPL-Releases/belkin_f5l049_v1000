dnl
dnl Change our version number here every time we release a new tarball
dnl
AC_INIT(dhcpcd, 2.0.4)
AC_CONFIG_SRCDIR(src/dhcpcd.c)
AC_CONFIG_FILES(Makefile src/Makefile)
AC_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(dist-bzip2)

dnl
dnl Enable extra options via configure 
dnl
AC_ARG_ENABLE(debug,
	      AC_HELP_STRING([--enable-debug], [Enables extra debug messages]),
	      enable_debug="${enableval}", enable_debug="no")
if test "${enable_debug}" = "yes"; then
    AC_DEFINE(DEBUG, , [Define to enable extra debug messages])
fi

AC_ARG_ENABLE(arp,
	      AC_HELP_STRING([--enable-arp],
			     [Use ARP to check if IP address already exists]),
	      enable_arp="${enableval}", enable_arp="no")
if test "${enable_arp}" = "yes"; then
    AC_DEFINE(ARPCHECK, , [Define to enable ARP checking of IP address])
fi

AC_ARG_ENABLE(embed,
	      AC_HELP_STRING([--enable-embed],
			     [vfork() instead of fork() for embedded environs]),
	      enable_embed="${enableval}", enable_embed="no")
if test "${enable_embed}" = "yes"; then
    AC_DEFINE(EMBED, , [Define to use vfork() instead of fork()])
fi

dnl
dnl Checks for programs.
dnl
AC_PROG_CC
AC_PROG_INSTALL

dnl
dnl Add the -Wall flag if we're using GCC
dnl
if test "$GCC" = "yes"; then
    CFLAGS="${CFLAGS} -Wall"
fi

dnl
dnl Checks for header files.
dnl
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h paths.h sys/ioctl.h sys/time.h syslog.h unistd.h \
		 arpa/inet.h netdb.h netinet/in.h sys/socket.h, , \
		 echo "Required header file not found"
		 exit 1
		 )

dnl
dnl Checks for typedefs, structures, and compiler characteristics.
dnl
AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_HEADER_TIME
AC_C_CONST
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_FUNC_SELECT_ARGTYPES
AC_HEADER_SYS_WAIT 
dnl
dnl Checks for library functions.
dnl
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_CHECK_FUNCS(select socket uname alarm dup2 gethostbyaddr gethostname \
	       gettimeofday memmove memset mkdir sethostname strdup strtol, , \
	       echo "Required function not found"
	       exit 1
	       )

AC_OUTPUT
